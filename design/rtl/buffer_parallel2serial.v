// ----------------------------------------------------------------------------------------------------------------------------
// Module Name: buffer_parallel2serial.v
// ----------------------------------------------------------------------------------------------------------------------------
// Description:
//
// ----------------------------------------------------------------------------------------------------------------------------
// Revision History:
//      Date:         2026-01-14
//      Author:       A. Lema, F. Villar
//      Organization: Fundación Fulgor
// ----------------------------------------------------------------------------------------------------------------------------

module buffer_parallel2serial #(
    parameter NB_DATA   = 8
) (
    input                       i_clk,
    input                       i_rst,
    input                       i_clk_en,
    input                       i_valid,
    input                       i_tx_ready,
    input      signed [NB_DATA-1:0] i_data0_re,
    input      signed [NB_DATA-1:0] i_data0_im,
    input      signed [NB_DATA-1:0] i_data1_re,
    input      signed [NB_DATA-1:0] i_data1_im,
    input      signed [NB_DATA-1:0] i_data2_re,
    input      signed [NB_DATA-1:0] i_data2_im,
    input      signed [NB_DATA-1:0] i_data3_re,
    input      signed [NB_DATA-1:0] i_data3_im,
    input      signed [NB_DATA-1:0] i_data4_re,
    input      signed [NB_DATA-1:0] i_data4_im,
    input      signed [NB_DATA-1:0] i_data5_re,
    input      signed [NB_DATA-1:0] i_data5_im,
    input      signed [NB_DATA-1:0] i_data6_re,
    input      signed [NB_DATA-1:0] i_data6_im,
    input      signed [NB_DATA-1:0] i_data7_re,
    input      signed [NB_DATA-1:0] i_data7_im,
    output reg signed [NB_DATA-1:0] o_data_re,
    output reg signed [NB_DATA-1:0] o_data_im,
    output reg                  o_valid
);

reg signed [NB_DATA-1:0] mem_re [0:31];
reg signed [NB_DATA-1:0] mem_im [0:31];

localparam S_LOADING   = 0; // Filling memory from FFT
localparam S_WAIT_RDY  = 1; // Waiting for TX to be ready
localparam S_SEND_ITEM = 2; // Pulsing valid for 1 cycle
localparam S_WAIT_BSY  = 3; // Waiting for TX to acknowledge (go busy)

reg [1:0] state;
reg [1:0] batch_count;
reg [4:0] read_ptr;

always @(posedge i_clk) begin
    if (i_rst) begin
        state       <= S_LOADING;
        batch_count <= 0;
        read_ptr    <= 0;
        o_valid     <= 0;
    end 
    else if (i_clk_en) begin
        case (state)
            S_LOADING: begin
                o_valid <= 1'b0;
                if (i_valid) begin
                    mem_re[{batch_count, 3'd0}] <= i_data0_re; mem_im[{batch_count, 3'd0}] <= i_data0_im;
                    mem_re[{batch_count, 3'd1}] <= i_data1_re; mem_im[{batch_count, 3'd1}] <= i_data1_im;
                    mem_re[{batch_count, 3'd2}] <= i_data2_re; mem_im[{batch_count, 3'd2}] <= i_data2_im;
                    mem_re[{batch_count, 3'd3}] <= i_data3_re; mem_im[{batch_count, 3'd3}] <= i_data3_im;
                    mem_re[{batch_count, 3'd4}] <= i_data4_re; mem_im[{batch_count, 3'd4}] <= i_data4_im;
                    mem_re[{batch_count, 3'd5}] <= i_data5_re; mem_im[{batch_count, 3'd5}] <= i_data5_im;
                    mem_re[{batch_count, 3'd6}] <= i_data6_re; mem_im[{batch_count, 3'd6}] <= i_data6_im;
                    mem_re[{batch_count, 3'd7}] <= i_data7_re; mem_im[{batch_count, 3'd7}] <= i_data7_im;
                    if (batch_count == 2'd3) begin
                        batch_count <= 0;
                        read_ptr    <= 0;
                        state       <= S_WAIT_RDY;
                    end 
                    else begin
                        batch_count <= batch_count + 1'b1;
                    end
                end
            end
            S_WAIT_RDY: begin
                o_valid <= 1'b0;
                if (i_tx_ready) begin
                    state <= S_SEND_ITEM;
                end
            end
            S_SEND_ITEM: begin
                o_data_re <= mem_re[read_ptr];
                o_data_im <= mem_im[read_ptr];
                o_valid   <= 1'b1;
                state     <= S_WAIT_BSY;
            end
            S_WAIT_BSY: begin
                o_valid <= 1'b0;
                if (!i_tx_ready) begin
                    if (read_ptr == 5'd31) begin
                        state    <= S_LOADING;
                        read_ptr <= 0;
                    end 
                    else begin
                        read_ptr <= read_ptr + 1'b1;
                        state    <= S_WAIT_RDY;
                    end
                end
            end
        endcase
    end
end

endmodule